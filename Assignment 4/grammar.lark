?start: exp

?exp: exp "+" exp1             -> plus
      | exp "-" exp1          -> minus
      | exp1

?exp1: exp1 "*" exp2          -> mul
      | exp1 "/" exp2         -> div
      | exp2

?exp2: "\\" NAME "." exp             -> lam
      | exp3   
?exp3: exp3 exp4                    -> app
      | exp4
?exp4: NAME                         -> var
      | NUMBER                      -> number
      | "-" exp4               -> neg
      | "(" exp ")"   
    

NAME: /[a-z_][a-zA-Z0-9_]*/
NUMBER: /\d+(\.\d+)?/    


%import common.WS
%ignore WS

%ignore /\/\/[^\n]*/  
%ignore /\/\*[\s\S]*?\*\//  
